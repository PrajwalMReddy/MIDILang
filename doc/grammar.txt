----------------------------META-GRAMMAR-----------------------------

Key:
    | "?" : Zero Or One Times
    | "*" : Zero Or More Times
    | "+" : One Or More Times

-------------------------------GRAMMAR-------------------------------

file:
    | program EOF

program:
    | import_statement* statement*

import_statement:
    | "import" ":" IDENTIFIER ";"

statement:
    | declaration_statement
    | action_statement

declaration_statement:
    | tune_statement
    | variable_statement

tune_statement:
    | "tune" IDENTIFIER (":" IDENTIFIER ("," IDENTIFIER)*)? "{" statement* "}"

variable_statement:
    | "var" IDENTIFIER ":" NUMBER ";"

action_statement:
    | loop_statement
    | play_statement

loop_statement:
    | "loop" ":" NUMBER "{" statement* "}"

play_statement:
    | "play" ":" NOTE DURATION VELOCITY ";"
    | "play" IDENTIFIER (":" NUMBER ("," NUMBER)*)? ";"
