----------------------------META-GRAMMAR-----------------------------

Key:
    | "?" : Zero Or One Times
    | "*" : Zero Or More Times
    | "+" : One Or More Times

-------------------------------GRAMMAR-------------------------------

file:
    | program EOF

program:
    | import_statement* statement*

import_statement:
    | "import" IDENTIFIER ";"

statement:
    | declaration_statement
    | action_statement

declaration_statement:
    | tune_statement
    | variable_statement

tune_statement:
    | "tune" IDENTIFIER (":" IDENTIFIER*)? "{" statement* "}"

variable_statement:
    | "var" IDENTIFIER "=" expression ";"

action_statement:
    | loop_statement
    | if_statement
    | play_statement
    | note_statement
    | assignment_statement

loop_statement:
    | "loop" ":" expression "{" statement* "}"

if_statement:
    | "if" ":" expression "{" statement* "}" ("else" (if_statement | "{" statement* "}"))?

play_statement:
    | "play" IDENTIFIER (":" expression*)? ";"

note_statement:
    | "note" expression expression expression ";"

assignment_statement:
    | IDENTIFIER "=" expression ";"

expression:
    | binary_expression
    | IDENTIFIER
    | NUMBER
    | BOOLEAN

binary_expression:
    | NUMBER ("+" | "-" | "*" | "/") NUMBER
